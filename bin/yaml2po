#!/usr/bin/env perl
package App::yaml2po;

use strict;
use warnings;

use Getopt::Long ();
use YAML::Syck ();

sub kv_to_string {
	my ($k, $v) = @_;

	foreach my $s ($k, $v) {
		$s =~ s/\\/\\\\/g;
		$s =~ s/"/\\"/g;
		$s =~ s/\n/\\n/g;
	}

	return "msgid \"$k\"\nmsgstr \"$v\"\n";
}

sub process {
	my ($fh) = @_;
	local $YAML::Syck::SortKeys = 1;
	local $YAML::Syck::LoadBlessed = 0;

	my $data = YAML::Syck::Load(do { local $/; <$fh> });
	my $text = '';
	foreach my $k (sort keys %$data) {
		$text .= kv_to_string($k, $data->{$k}) . "\n";
	}
	return $text;
}

sub script {
	my (@args) = @_;
	my $help;

	Getopt::Long::GetOptionsFromArray(
		\@args,
		"help" => \$help
	) or return usage(1);
	return usage(0) if $help;

	local @ARGV = @args;
	print process(\*ARGV);
	return 0;
}

sub usage {
	my $ret = shift;
	print <<EOM;
yaml2po [FILE]

Process the specified FILE (or standard input) as YAML and write a gettext
format po file to standard output.
EOM
	return $ret;
}

exit script(@ARGV) unless caller;
